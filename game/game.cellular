[properties]

Tile(Rock | Cave | Building)

Light(0..5)
Vein(OreType)
Dig(0..1)

Foreground(Imp | ResourceType | Empty)
Background(Scaffold | Empty)

BuildingVariant(Chest)

DirectionH(Left | Right)
DirectionHV(Left | Right | Up | Down)

Content(ResourceType)
SmallContentCount(0..10)


[materials]

Rock { Light Vein Dig }
Cave { Foreground Background }
Building { BuildingVariant }

Empty

Left
Right
Up
Down

RockVein
IronVein
CoalVein

RockOre
IronOre
CoalOre

Scaffold { DirectionHV }

Imp { DirectionH Content }

Chest { Content SmallContentCount }


[types]

OreType = RockOre | IronOre | CoalOre.
ResourceType = OreType.


[function rotate(direction value) value]

transform()
: 1 => (direction : Left => Right ; Right => Left ; _ => direction)
; 2 => (direction : Up => Down ; Down => Up ; _ => direction)
; 90 => (direction : Left => Down ; Right => Up ; Up => Left ; Down => Right)
; 180 => (direction : Left => Right ; Right => Left ; Up => Down ; Down => Up)
; 270 => (direction : Left => Up ; Right => Down ; Up => Right ; Down => Left)
; _ => direction


[group rockLightGroup @90 @180 @270]

[rule rockLightBoundary]

a Rock.
b.
--
(b : Rock => 0 ; _ => 1) ->
a Light(5).
b.

[rule rockLight]

a Rock Light(x).
b Rock Light(y).
-- x + 1 < y ->
a Light(x + 1).
b.


[group impDigGroup]

[rule impDig @90 @180 @270]

a Rock Vein(ore) Dig(1).
b Foreground(i Imp Content(Empty)).
--
Cave Foreground(i Content(ore)) Background(Scaffold DirectionHV(rotate(Up))).
b Foreground(Empty).
