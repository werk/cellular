[properties]

Tile(Rock | Cave | Building)

Light(0..5)
Vein(OreType)
Dig(0..1)

Foreground(Imp | ResourceType | Empty)
Background(Scaffold | Empty)

BuildingVariant(Chest)

DirectionH(Left | Right)
DirectionHV(Left | Right | Up | Down)

Content(ResourceType | Empty)
SmallContentCount(0..10)


[materials]

Rock { Light Vein Dig }
Cave { Foreground Background }
Building { BuildingVariant }

Empty

Left
Right
Up
Down

RockOre
IronOre
CoalOre

Scaffold { DirectionHV }

Imp { DirectionH Content }

Chest { Content SmallContentCount }


[types]

OreType = RockOre | IronOre | CoalOre.
ResourceType = OreType.


[function rotate(direction value) value]

getTransform()
: 1 =>   (direction : Left => Right ; Right => Left ; _ => direction)
; 2 =>   (direction : Up   => Down  ; Down  => Up   ; _ => direction)
; 90 =>  (direction : Left => Down  ; Right => Up   ; Up => Left  ; Down => Right)
; 180 => (direction : Left => Right ; Right => Left ; Up => Down  ; Down => Up)
; 270 => (direction : Left => Up    ; Right => Down ; Up => Right ; Down => Left)
; _ => direction


[group generateGroup]

[rule generateCave]

_.
-- getStep() == 0 ->
getTileX() > 5 && getTileX() < 15 && getTileY() > 5 && getTileY() < 15
    -> Cave Foreground(Empty) Background(Empty).
    | Rock Light(0) Vein(RockOre) Dig(0).

[rule generateImp1 Foreground]

_.
-- getStep() == 0 && getTileX() == 9 && getTileY() == 7 ->
Imp DirectionH(Left) Content(Empty).

[rule generateImp2 Foreground]

_.
-- getStep() == 0 && getTileX() == 11 && getTileY() == 6 ->
Imp DirectionH(Right) Content(Empty).


[group rockLightGroup @r]

[rule rockLightBoundary]

a Rock.
b.
--
(b : Rock => 0 ; _ => 1) ->
a Light(5).
b.

[rule rockLight]

a Rock Light(x).
b Rock Light(y).
-- x + 1 < y ->
a Light(x + 1).
b.


[group impDigGroup]

[rule impDig @r]

a Rock Vein(ore) Dig(1).
b Foreground(i Imp Content(Empty)).
--
Cave Foreground(i Content(ore)) Background(Scaffold DirectionHV(rotate(Up))).
b Foreground(Empty).


[group impMoveGroup]

[rule impWalk @h]

_, _.
a Foreground(i Imp DirectionH(d)), b Foreground(Empty).
Rock, Rock.
-- d === rotate(Right) ->
a Foreground(Empty), b Foreground(i).

[rule impFall]

a Foreground(i Imp).
b Foreground(Empty).
--
a Foreground(Empty).
b Foreground(i).

[rule impSwap]

a Foreground(i1 Imp DirectionH(Right)), b Foreground(i2 Imp DirectionH(Left)).
--
a Foreground(i2), b Foreground(i1).

[rule impTurn !impWalk !impFall !impSwap @h]

a Foreground(i Imp DirectionH(d)), b Rock.
-- d === rotate(Right) ->
a Foreground(i DirectionH(rotate(Left))), b.
